// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/02/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the output bits:
 * zr = (out == 0, 1, 0)
 * ng = (out < 0,  1, 0)
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not
CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // (out == 0, 1, 0)
        ng;      // (out < 0,  1, 0)

    PARTS:
    //// Replace this comment with your code.

	
	Mux16(a=x, b=false, sel=zx, out=mux1);
	Not16(in=mux1, out=not1);
	Mux16(a=mux1, b=not1, sel=nx, out=f1);

	Mux16(a=y, b=false, sel=zy, out=mux2);
	Not16(in=mux2, out=not2);
	Mux16(a=mux2, b=not2, sel=ny, out=f2);

	Add16(a=f1, b=f2, out=add1);
	And16(a=f1, b=f2, out=and1);
	Mux16(a=and1, b=add1, sel=f, out=mux3);
	Not16(in=mux3, out=not3);
	Mux16(a=mux3, b=not3, sel=no, out[15]=ng, out[0..7]=zeros1, out[8..15]=zeros2, out=out);
	
	Or8Way(in=zeros1, out=or1);
	Or8Way(in=zeros2, out=or2);
	Or(a=or1, b=or2, out=zeros3);
    Not(in=zeros3, out=zr);


}